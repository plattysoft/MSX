1 '** -Ice Hopper- by Hakogame
2 '** This is my attempt to make a platform game in pure MSX BASIC
3 '** I will try to explain my code as completely as possible
4 '* Start off by setting the screen to mode1 and setting all variables as integers to speed things up a bit
5 SCREEN1,2,0:COLOR15,1,1:WIDTH32:KEYOFF:DEFINTA-Z
6 '* Poke a couple sprites into memory
7 FORI=&H3860TO&H3864:READA$:VPOKEI,VAL("&H"+A$):NEXT:DATA38,78,74,88,70
8 FORI=&H3880TO&H3882:READA$:VPOKEI,VAL("&H"+A$):NEXTI:DATA3,7,88
9 '* Poke graphical characters 64 through 80 [first character number*8 to (last character number*8)+7]
10 FORI=512TO647:READA$:VPOKEI,VAL("&H0"+A$):NEXT
11 '* Play the Hakogame tune and put the "HA" and Turtle shell on the screen
12 PLAY"t120l4o4agbaa","t120l4o4ffggg":LOCATE12,12:PRINT"C@";:PUTSPRITE1,(128,89),6,3
13 '* Poke more graphical characters into memory and print text after each section to try and sync the letters to the music 
14 FORI=264TO327:READA$:VPOKEI,VAL("&H"+A$):NEXT:PRINT"EH";
15 FORI=384TO511:READA$:VPOKEI,VAL("&H"+A$):NEXT:PRINT"B@";
16 FORI=704TO751:READA$:VPOKEI,VAL("&H"+A$):NEXT:PRINT"GA"
17 FORI=768TO775:READA$:VPOKEI,VAL("&H"+A$):NEXT
18 FORI=832TO863:READA$:VPOKEI,VAL("&H"+A$):NEXT
19 FORI=896TO983:READA$:VPOKEI,VAL("&H"+A$):NEXT
20 '* place the turtle sprites and a black sprite to cover the character at 31,23 because we can't print there without moving the whole screen up one row
21 PUTSPRITE1,(128,88):PUTSPRITE2,(128,91),12,4:PUTSPRITE3,(248,183),1,5
22 '* Poke the rest of the sprites
23 FORI=&H3800TO&H3818:READA$:VPOKEI,VAL("&H"+A$):NEXT
24 FORI=&H3820TO&H3838:READA$:VPOKEI,VAL("&H"+A$):NEXT
25 FORI=&H3840TO&H3845:READA$:VPOKEI,VAL("&H"+A$):NEXT
26 FORI=&H38A0TO&H38A7:VPOKEI,VAL("&HFF"):NEXT
27 FORI=&H38C0TO&H38D9:READA$:VPOKEI,VAL("&H"+A$):NEXT
28 '* Here, we set the character colors. In screen 1, you can set the FG & BG colors for each group of 8 characters: base(6)+[character code\8]
29 FORI=8196TO8207:READA$:VPOKEI,VAL(A$):NEXT
30 '* Set some variables and gosub to where we have all the stage layouts
31 V=1:W=2:X=128:Y=160:GOSUB102
32 '* Set the rest of the colors, place some sprites, and turn the screen back on.
33 FORI=8196TO8207:READA$:VPOKEI,VAL(A$):NEXT:PUTSPRITE1,(184,184):PUTSPRITE2,(184,187):PUTSPRITE0,(76,22),15,0:PUTSPRITE4,(164,22),15,0:PUTSPRITE5,(171,20),8,2:VDP(1)=VDP(1)OR&B01000000
34 '* Title screen music. Unfortunately, the whole program needs to pause for a couple seconds while the music plays. 
35 PLAY"o4l6t255brgrbrgrarfraaaarbrgrbrgrarfrddrrfrdrfrdrgrbraaaarbrgrbrgrfrdrggg","o3l6t255drbrdrbrdrbrddddrbrdrbrdrbrdrbbrrv0drbrdrbrdrbrddddrv8drbrdrbrdrbrbbb":LOCATE7,16:PRINT"IJAKK?EAO?HJ?KLD<E":LOCATE12,17:PRINT"LH?IF@O="
36 '* Check for button press. Q is used to implement keyboard or joystick control. Beep lets you stop the music.
37 IFSTICK(0)>0ORSTRIG(0)<0THENQ=0:BEEP:GOTO40ELSEIFSTICK(1)>0ORSTRIG(1)<0THENQ=1:BEEP:GOTO40
38 GOTO37
39 '* Make stage 1 ready for play
40 LOCATE7,16:PRINT"                  ";:LOCATE12,17:PRINT"        ":LOCATE13,10:PRINT"BAL?LCA":LOCATE9,11:PRINT"KL@JK?LH?IJHNA":LOCATE11,12:PRINT"OHMJ?FHNA=":PUTSPRITE4,,,1:LOCATE10,1:PRINT"hkkkkkkkkkkh":LOCATE9,5:PRINT"@G@kwrtpukkIJH"
41 PUTSPRITE4,(0,192):PUTSPRITE5,(0,192):PUTSPRITE0,(0,192)
42 '*Remove "press key" text, print status bar and play a few notes.
43 LOCATE0,23:PRINT"?????>KL@BA:"+RIGHT$("0"+MID$(STR$(V),2,2),2)+">??>FDNAK:"+RIGHT$("0"+MID$(STR$(W),2,2),2)+">????";:PUTSPRITE1,(0,192):PUTSPRITE2,(0,192):PLAY"o4l6t255brgrggggg","o3l6t255drbrbbbbb"
44 '** Sprite Movement Loop. Comments at end of loop **
45 IFZ<8THENZ=Z+1:GOTO47ELSE47
46 IFABS(C)<5ANDABS(STICK(Q)-5)=2THENGOSUB57ELSEA=A-SGN(A)
47 B=B+A:IFABS(B)>4THENGOSUB58ELSEC=0
48 IFSTRIG(Q)=-1ANDE=1THENGOSUB54
49 Y=Y+Z:X=X+C:D=VPEEK(6176+(Y\8)*32+X\8):IFZ>0ANDD=40THENGOSUB59ELSEIFD=96ANDZ>0THENGOSUB55:GOTO51
50 IFD=104THEN78ELSEIFD=80THENGOSUB56:GOTO51ELSEIFD\4=9THEN71ELSEIFD<>40THENE=0:PUTSPRITE0,,,0:GOTO51
51 PUTSPRITE0,(X-8,Y-2)
52 IFE=0THEN45ELSE46
53 '* These are all GOSUB's because it makes the main loop a bit faster.
54 E=0:Z=-7:PLAY"o3l8t255a-bb":RETURN
55 PLAY"o2l8t255co3egg":Z=-11:RETURN
56 PLAY"o2l8t255a":A=A*-1:X=X-C:B=B*-1:Z=Z-2:RETURN
57 A=A-(STICK(Q)-5):RETURN
58 C=B\5:B=B-C*5:RETURN
59 E=1:Z=0:Y=(Y\8)*8:PUTSPRITE0,,,1:RETURN
60 '* This loop is pretty well optimized, I think
61 '* Line 45: Z is the "gravity" variable. This line is only called when the player is in the air, and while in the air, you can't change your movement speed so jump to 47.
62 '* Line 46: If your are pressing either left or right, add to speed variable A. Otherwise, apply friction (subtract from speed variable A)
63 '* Line 47: Add A to B, and if B>4, then C=B\5 and B=B-C*5. This allows a fake "sub-pixel" movement.
64 '* Line 48: Jump if you press space or the joystick button only if you are on a platform (Value E=1)
65 '* Line 49: Set the X position and Y position variables (X and Y), then find which Character the player is standing on (Value D).
66 '* Line 49 continued: If the player is on a platform then stop falling. If the player is on a spring then bounce upwards. Jump past the other conditions and set the sprite position
67 '* Line 50: If you grab the star, jump out of the movement loop. If the player hits a wall then bounce. If the player hits a spike then jump out of the movement loop. If the player is not on a platform then fall.
68 '* Line 51: move the player to current X/Y coordinates
69 '* Line 52: If you're airborne goto 45, otherwise save some time and skip to 46
70 '* Death Sequence
71 PLAY"o2l16t255bfedd-d-":Z=-12:Y=Y*6:A=0:B=0:C=0:LOCATE13,6:PRINT"ODEAK="
72 PUTSPRITE0,(X-8,(Y\6)-2),,6:Y=Y+Z:IFZ<16THENZ=Z+1
73 IFY>1200THENW=W-1
74 IFY>1200ANDW>-1THENVDP(1)=VDP(1)AND&B10111111:GOSUB102:VDP(1)=VDP(1)OR&B01000000:IFV=1THENGOTO40ELSE43
75 IFW<0THENLOCATE9,10:PRINT"??????????????";:LOCATE9,11:PRINT"?>B@GA?HNAJ=>?";:LOCATE9,12:PRINT"??????????????";:PLAY"o3l6t180brgrggggg","o2l6t180drbrbbbbb":FORI=0TO6000:NEXT:RESTORE:GOTO1
76 GOTO72
77 '* You got a Star!
78 G=0:TIME=0:PUTSPRITE0,(X-8,Y-2):X=X\8:Y=Y\8:PLAY"o7l64t255cdceev7ev6ev8","o6l64t255efeggv7gv6gv8"
79 IFV=1THENK=(X\10)-1
80 IFK=1THENM=(VMOD2=0)*-1ELSEM=1
81 IFHMOD10=0THENLOCATEX-1,Y:PRINT"jij";:LOCATEX-1,Y+1:PRINT"ihi";:LOCATEX-1,Y+2:PRINT"jij";:G=G+1
82 IFHMOD10=5THENLOCATEX-1,Y:PRINT"iji";:LOCATEX-1,Y+1:PRINT"jhj";:LOCATEX-1,Y+2:PRINT"iji";:G=G+1
83 IFG=2THENLOCATE 9,5:PRINT"OHM?BHL?@?KL@J=":G=G+1
84 IFG=7THENPLAY"o4l8t255cgab":READA:READA$:LOCATE A,7:PRINTA$:G=G+1
85 IFG=12ANDM=1THENPLAY"o5l8t255cgab":LOCATE 14,9:PRINT"1MI=":G=G+1
86 IFG=17THENVDP(1)=VDP(1)AND&B10111111:CLS:V=V+1:W=W+M:GOSUB102:VDP(1)=VDP(1)OR&B01000000:GOTO43
87 H=TIME\4:GOTO81
88 '* All DATA goes here
89 DATA0,3C,62,62,7E,62,62,0,0,7E,60,7C,60,60,7E,0,0,3C,62,60,6E,66,3C,0,0,62,62,7E,62,62,62,0,0,7E,18,18,18,18,7E,0,0,62,64,78,68,64,62,0,0,60,60,60,60,60,7E,0,0,76,6A,6A,6A,62,62,0,0,3C,62,62,62,62,3C,0,0,7C,62,62,7C,60,60,0,0,7C,62,62,7C,62,62
90 DATA0,0,3C,62,38,E,46,3C,0,0,7E,18,18,18,18,18,0,0,62,62,62,62,62,3C,0,0,62,62,34,34,18,8,0,0,62,62,34,18,18,18,0,60,42,6,E,1E,3C,78,70,0,80,80,80,80,80,80,0,0,81,81,81,81,81,81,0,0,1,1,1,1,1,1,0,8,8,1C,1C,3E,3E,7F,7F,3,F,3F,FF,3F,F,3
91 DATA0,FE,FE,7C,7C,38,38,10,10,0,C0,F0,FC,FF,FC,F0,C0,0,D9,B3,67,CE,C,0,0,0,3C,66,6A,72,62,3C,0,0,38,58,18,18,18,7C,0,0,3C,46,6,C,30,7E,0,0,7C,6,1C,6,6,7C,0,0,64,64,64,7E,4,4,0,0,7C,40,7C,6,46,3C,0,0,3C,40,7C,46,46,3C,0,0,7E,42,6,C,18,18,0,0,3C
92 DATA46,3C,46,46,3C,0,0,3C,62,62,3E,2,3C,0,0,0,30,30,0,30,30,0,3C,42,99,A1,A1,99,42,3C,0,3C,62,60,60,62,3C,0,0,18,18,30,30,0,60,0,0,0,0,3C,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,7E,7E,0,0,0,0,0,0,0,0,0,0,0,FF,FF,FF,FF,FF,FF,FF,FF,0,0,0,0,C,1E,1F
93 DATA1F,F,7,3,1,60,F0,F0,F0,E0,C0,80,0,0,42,3C,42,3C,42,3C,0,24,30,BF,3E,7C,FD,C,24,10,42,10,1D,B8,8,42,8,0,0,8,20,4,10,0,0,0,0,0,0,0,0,0,0,FF,FF,3F,F,3,0,0,0,FF,FF,FF,FF,FF,FF,3C,0,FF,FF,FC,F0,C0,0,0,0,C0,0,0,0,0,0,0,0,FF,81,0,0,0,0,0,0,FF,FF
94 DATAFF,FF,FF,FF,3F,F,3,0,0,0,0,0,0,0,FF,FF,FF,FF,FF,FF,FC,F0,0,1,1,1,1,1,1,0,0,80,80,80,80,80,80,0,0,81,81,81,81,81,81,0
95 DATA6,F,9,19,1F,10,18,1C,F,0,0,0,0,0,0,0,60,F0,90,98,F8,8,18,38,F0,6,F,F,19,19,1F,18,1F,F,0,0,0,0,0,0,0,60,F0,F0,98,98,F8,18,F8,F0,18,38,8,7,7,2
96 DATA6,9,9,19,1F,1C,18,18,8,7,0,0,0,0,0,0,60,90,90,98,F8,38,18,18,10,E0
97 DATA17,17,17,17,241,241,17,17,17,17,17,17
98 DATA116,247,241,241,241,241,248,132,180,181,84,117
99 DATA14,A@KO=,14,<HHF=,14,@<A=,13,IJHIAJ=,12,OHM?JMFA=,13,KMIAJ=,13,BJHHNO=,13,BJA@L=,13,<FANAJ=,12,BFHJDHMK=,11,G@JNAFHMK=,12,KLAFF@J=,11,JDBCLAHMK=,12,KM<<AKK=,11,ND<LHJDHMK=,12,LJDMGIC=
100 '** Here are all the stage layouts **
101 '* hide the sprites so they don't "pop" everytime a stage loads and set player speed to 0
102 PUTSPRITE0,(0,192):PUTSPRITE4,(0,192):PUTSPRITE5,(0,192):A=0:B=0:C=0
103 '* Generic stage BG & Borders used for every stage
104 LOCATE0,0:PRINT"PkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkPPkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkPPkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkPPkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkPPkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkPPwrtpukkkkkkkwrtpukkkkkkkwrtpukPPs   vpqrtpqrs   vpqrtpqrs   vpP"
105 FORI=7TO22:FORJ=0TO1:LOCATEJ*31,I:PRINT"P";:NEXT:NEXT
106 LOCATE0,22:PRINT"P((((((((((((((((((((((((((((((P";
107 '** Jump to selected stage
108 IFV=2THEN131
109 IFV=3THEN135
110 IFV=4ANDK=1THEN139ELSEIFV=4ANDK=0THEN143
111 IFV=5ANDK=1THEN143ELSEIFV=5ANDK=0THEN149
112 IFV=6ANDK=1THEN149ELSEIFV=6ANDK=0THEN158
113 IFV=7ANDK=1THEN153ELSEIFV=7ANDK=0THEN182
114 IFV=8ANDK=1THEN158ELSEIFV=8ANDK=0THEN187
115 IFV=9ANDK=1THEN163ELSEIFV=9ANDK=0THEN192
116 IFV=10ANDK=1THEN167ELSEIFV=10ANDK=0THEN197
117 IFV=11ANDK=1THEN172ELSEIFV=11ANDK=0THEN202
118 IFV=12GOTO178
119 IFV=13GOTO182
120 IFV=14GOTO187
121 IFV=15GOTO192
122 IFV=16GOTO197
123 IFV=17GOTO202
124 '** Stage1: title/practice
125 X=128:Y=160:Z=0:PUTSPRITE0,,,0:LOCATE13,7:PRINT"#((((!":LOCATE1,22:PRINT"$$$$";:LOCATE7,4:PRINT"x(((((ykkkkx(((((y":LOCATE3,17:PRINT"&&":LOCATE8,20:PRINT"&&"
126 LOCATE0,9:PRINT"P      #((($$$((((((((((!      PP((((!  P??????????????P  #((((PP&&     P              P       PP      %P":LOCATE23,12:PRINT"P       PP  #((!%P((((((((((((((P((((!  PP       Y  & & &&&& &  Y"
127 LOCATE1,16:PRINT"#((((!":LOCATE25,18:PRINT"#((((!";:LOCATE6,19:PRINT"#((((!":LOCATE20,20:PRINT"#(((((!":LOCATE13,21:PRINT"#((((!"
128 '* the VDP(1)AND condition turns the screen off and VDP(1)OR turns it back on. This way you don't see the screen drawing in, and it might be faster too.
129 VDP(1)=VDP(1)AND&B10111111:PUTSPRITE1,(0,192):PUTSPRITE2,(0,192):LOCATE9,11:PRINT"?>D<A?CHIIAJ>?";:IFW=2THENLOCATE9,12:PRINT"??????????????";:LOCATE0,23:PRINT"????????;2022?C@EHB@GA?????????";
130 RETURN
131 '** Stage 2
132 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE5,1:PRINT"h":LOCATE3,4:PRINT"x((((y":LOCATE5,8:PRINT"ZZ":LOCATE5,9:PRINT"``":LOCATE3,10:PRINT"#(((((((!":LOCATE13,15:PRINT"ZZ":LOCATE13,16:PRINT"``("+CHR$(34)+"("+CHR$(34)+"(((((!":LOCATE28,21:PRINT"ZZ"
133 LOCATE28,22:PRINT"``":LOCATE14,22:PRINT"$$$$":LOCATE20,17:PRINT"&&&":LOCATE5,11:PRINT"&&&&&"
134 RETURN
135 '** Stage 3
136 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE25,1:PRINT"h":FORI=8TO22:LOCATE8,I:PRINT"P":NEXT:FORI=0TO16:LOCATE11,I:PRINT"P":NEXT:LOCATE6,22:PRINT"``":LOCATE1,13:PRINT"``":LOCATE27,22:PRINT"$$$$":LOCATE16,19:PRINT"#((((((!":LOCATE24,16:PRINT"#(((((!"
137 LOCATE18,13:PRINT"#((((!":LOCATE24,10:PRINT"#(((!":LOCATE20,7:PRINT"#((!":LOCATE24,4:PRINT"x((y":LOCATE6,21:PRINT"ZZ":LOCATE1,12:PRINT"ZZ":LOCATE8,7:PRINT"X":LOCATE11,17:PRINT"Y":LOCATE19,20:PRINT"&&":LOCATE26,17:PRINT"&&&":LOCATE20,14:PRINT"&&"
138 RETURN
139 '** Stage 4
140 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE15,1:PRINT"h":LOCATE13,4:PRINT"x(((y":LOCATE13,7:PRINT"#((((!#((((((((P":LOCATE28,8:PRINT"P":LOCATE28,9:PRINT"P":LOCATE1,12:PRINT"(((((!#((((!#((((!#((((!#(((((":LOCATE28,6:PRINT"X":LOCATE28,10:PRINT"Y"
141 LOCATE1,17:PRINT"ZZ":LOCATE1,18:PRINT"``":LOCATE1,19:PRINT"((!":LOCATE3,22:PRINT"$$$$$$$$$$$$$$$$$$$$$$$$$$$$":LOCATE4,13:PRINT"&    &&    &&    &&    &&&&"
142 RETURN
143 '** STAGE 5
144 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE27,1:PRINT"h"
145 LOCATE5,22:PRINT"$$((((($$((((($$":FORI=10TO15:LOCATE29,I:PRINT"P":NEXT:FORI=15TO18:LOCATE28,I:PRINT"P":NEXT:LOCATE2,16:PRINT"#(((($$$(((((((((((((!":FORI=9TO11:LOCATE4,I:PRINT"%P":NEXT:LOCATE1,12:PRINT"((('Y":LOCATE1,8:PRINT"(((``"
146 LOCATE14,7:PRINT"```":LOCATE25,10:PRINT"#(((":LOCATE22,3:PRINT"x((((((":FORI=0TO5:LOCATE29,I:PRINT"P":NEXT:LOCATE29,9:PRINT"X":LOCATE29,16:PRINT"Y":LOCATE28,19:PRINT"Y":LOCATE28,14:PRINT"X":LOCATE4,7:PRINT"ZZ":LOCATE14,6:PRINT"ZZZ"
147 LOCATE13,8:PRINT"#(((!":LOCATE29,6:PRINT"Y":LOCATE27,11:PRINT"&&":LOCATE15,9:PRINT"&"
148 RETURN
149 '** Stage 6: Springs!
150 X=32:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE3,1:PRINT"h":LOCATE25,22:PRINT"``":LOCATE25,15:PRINT"``":LOCATE13,15:PRINT"`````":LOCATE1,14:PRINT"``":LOCATE22,9:PRINT"````":LOCATE14,3:PRINT"x((y":LOCATE9,8:PRINT"``"
151 LOCATE1,22:PRINT"$$":LOCATE29,22:PRINT"$$":LOCATE25,21:PRINT"ZZ":LOCATE25,14:PRINT"ZZ":LOCATE13,14:PRINT"ZZZZZ":LOCATE1,13:PRINT"ZZ":LOCATE22,8:PRINT"ZZZZ":LOCATE9,7:PRINT"ZZ"
152 RETURN
153 '** Stage 7
154 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE27,1:PRINT"h":FORI=10TO20:LOCATE28,I:PRINT"P":NEXT:LOCATE29,22:PRINT"``":LOCATE12,22:PRINT"``$$$$$$$$$$":LOCATE 17,12:PRINT"#(((((((((!":LOCATE14,13:PRINT"#((!":LOCATE11,14:PRINT"#((!":LOCATE8,15:PRINT"#((!"
155 LOCATE5,16:PRINT"#((!":LOCATE2,17:PRINT"``":LOCATE2,16:PRINT"ZZ":LOCATE5,8:PRINT"#((((!":LOCATE15,7:PRINT"#((((!":LOCATE21,4:PRINT"x(((((y":LOCATE28,6:PRINT"#((":LOCATE29,21:PRINT"ZZ":LOCATE28,9:PRINT"X":LOCATE28,21:PRINT"Y":LOCATE12,21:PRINT"ZZ"
156 LOCATE19,13:PRINT"& &&& &":LOCATE7,9:PRINT"&&"
157 RETURN
158 '** Stage 8
159 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE3,1:PRINT"h":LOCATE1,4:PRINT"(((((y":LOCATE11,9:PRINT"ZZZ":LOCATE11,10:PRINT"```":LOCATE15,8:PRINT"#(((((((((!":FORI=9TO19:LOCATE21,I:PRINT"P'":NEXT
160 FORI=7TO22:LOCATE30,I:PRINT"%":NEXT:LOCATE26,9:PRINT"#(((":LOCATE23,13:PRINT"(((!":LOCATE26,16:PRINT"#(((":LOCATE23,19:PRINT"(((!":LOCATE10,21:PRINT"ZZ":LOCATE10,22:PRINT"``$$$$$$":LOCATE21,20:PRINT"Y"
161 LOCATE10,11:PRINT"#(((!":LOCATE12,12:PRINT"&":LOCATE17,9:PRINT"&&&&"
162 RETURN
163 '** Stage 9
164 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE27,1:PRINT"h":LOCATE5,22:PRINT"``((((``((((``((((``((((``":LOCATE5,21:PRINT"ZZ    ZZ    ZZ    ZZ    ZZ":LOCATE1,17:PRINT"&&&&&&&&&&&&&&&&&&&&&&&&&&":LOCATE1,16:PRINT"(((((((((((((((((((((((((((!"
165 LOCATE1,15:PRINT"$$$$$$$$$$$P":LOCATE12,14:PRINT"P&&&":LOCATE12,13:PRINT"P((((!":LOCATE12,12:PRINT"X":LOCATE2,10:PRINT"``(((! #(((!":LOCATE2,9:PRINT"ZZ":LOCATE6,4:PRINT"x((((ykkx((((ykkx((((y"
166 RETURN
167 '** Stage 10
168 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE27,1:PRINT"h":FORI=4TO14:LOCATE13,I:PRINT"P":NEXT:FORI=0TO10:LOCATE16,I:PRINT"P":NEXT:LOCATE9,3:PRINT"x((((y":LOCATE4,4:PRINT"x((y":LOCATE1,7:PRINT"((!":LOCATE4,10:PRINT"#(((!":LOCATE8,13:PRINT"#(((("
169 LOCATE1,16:PRINT"((((!":LOCATE4,19:PRINT"#(((!":LOCATE8,22:PRINT"$$$$$$$$$$$$$$$$$$$$$$$":LOCATE15,19:PRINT"#(((((((!":LOCATE26,16:PRINT"#((((":LOCATE18,13:PRINT"#((((!":LOCATE25,10:PRINT"#(((!":LOCATE20,7:PRINT"#((!":LOCATE24,4:PRINT"x(y"
170 LOCATE16,11:PRINT"Y":LOCATE13,15:PRINT"Y":LOCATE1,17:PRINT"&&":LOCATE11,14:PRINT"&&":LOCATE1,8:PRINT"&":LOCATE29,17:PRINT"&&":LOCATE20,14:PRINT"&&":LOCATE18,20:PRINT"&&&"
171 RETURN
172 '** Stage 11
173 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE26,1:PRINT"h":LOCATE4,22:PRINT"``":LOCATE11,17:PRINT"````":FORI=10TO12:LOCATE15,I:PRINT"P":NEXT:LOCATE6,15:PRINT"``":LOCATE1,9:PRINT"``":LOCATE8,5:PRINT"x(((y":LOCATE13,7:PRINT"``":LOCATE28,8:PRINT"``"
174 FORI=12TO14:LOCATE22,I:PRINT"P":NEXT:LOCATE16,21:PRINT"``":LOCATE14,15:PRINT"#(((((!":LOCATE1,17:PRINT"``":LOCATE24,20:PRINT"``":LOCATE26,16:PRINT"``":FORI=7TO11:LOCATE18,I:PRINT"P":NEXT:LOCATE19,11:PRINT"``":FORI=0TO3:LOCATE20,I:PRINT"P":NEXT
175 LOCATE22,4:PRINT"x(((((((y":FORI=5TO7:LOCATE23,I:PRINT"P":NEXT:LOCATE4,21:PRINT"ZZ":LOCATE11,16:PRINT"ZZZZ":LOCATE6,14:PRINT"ZZ":LOCATE1,8:PRINT"ZZ":LOCATE13,6:PRINT"ZZ":LOCATE28,7:PRINT"ZZ":LOCATE16,20:PRINT"ZZ":LOCATE1,16:PRINT"ZZ"
176 LOCATE24,19:PRINT"ZZ":LOCATE26,15:PRINT"ZZ":LOCATE19,10:PRINT"ZZ":LOCATE15,9:PRINT"X":LOCATE15,13:PRINT"Y":LOCATE22,11:PRINT"X":LOCATE22,15:PRINT"Y":LOCATE18,6:PRINT"X":LOCATE18,12:PRINT"Y":LOCATE23,8:PRINT"Y"
177 RETURN
178 '** Stage 12
179 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE28,2:PRINT"h":LOCATE9,22:PRINT"$$$$$$$$$$$$$$$$$$$$$$":LOCATE10,20:PRINT"#(((((!":LOCATE2,18:PRINT"#(((((!":LOCATE10,15:PRINT"#(((!":LOCATE2,13:PRINT"#((((!":FORI=9TO20:LOCATE1,I:PRINT"'":NEXT
180 LOCATE10,10:PRINT"#(((!":LOCATE1,8:PRINT"#((((!":LOCATE10,5:PRINT"x((((((y":LOCATE3,19:PRINT"&&&&&":LOCATE25,17:PRINT"````":LOCATE25,10:PRINT"```":FORI=0TO9:LOCATE24,I:PRINT"P":NEXT:LOCATE24,10:PRINT"Y":LOCATE25,16:PRINT"ZZZZ":LOCATE25,9:PRINT"ZZZ"
181 RETURN
182 '**stage 13
183 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE19,1:PRINT"h"
184 LOCATE6,22:PRINT"``$$$$$$$$$$(((((``((((``":LOCATE23,16:PRINT"&&&&&":LOCATE21,15:PRINT"#(((((((!":LOCATE13,14:PRINT"``":LOCATE1,14:PRINT"(((((!":LOCATE22,10:PRINT"#((((((!":LOCATE14,10:PRINT"``":LOCATE1,7:PRINT"(((((!   #((((!"
185 LOCATE18,5:PRINT"x(((((y":LOCATE13,13:PRINT"ZZ":LOCATE14,9:PRINT"ZZ":LOCATE23,21:PRINT"ZZ    ZZ":LOCATE6,21:PRINT"ZZ":LOCATE2,15:PRINT"&&&":LOCATE1,8:PRINT"&&"
186 RETURN
187 '** Stage 14
188 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE3,1:PRINT"h":LOCATE7,4:PRINT"x(z(z(z(z((yx(((ykx(((((":LOCATE1,10:PRINT"((((#(#(#(#(#(#(#(#(#(#(#(((('":LOCATE1,16:PRINT"%((((((!#(((!#(((!#(((!#((((((":LOCATE28,8:PRINT"ZZ%"
189 LOCATE28,9:PRINT"``%":LOCATE7,22:PRINT"$$$(((($$$(((($$$(((((('":LOCATE28,20:PRINT"ZZ%":LOCATE28,21:PRINT"``%":LOCATE1,14:PRINT"'ZZ":LOCATE1,15:PRINT"'``"
190 FORI=5TO26STEP2:LOCATEI,10:PRINTCHR$(34)+CHR$(40):NEXT
191 RETURN
192 '** Stage 15:leap of faith
193 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE15,1:PRINT"h":LOCATE7,22:PRINT"$$$$$$$$$$$$$$$$$$$$$$$$":FORI=17TO19:LOCATE3,I:PRINT"P":NEXT:LOCATE3,20:PRINT"Y":LOCATE1,16:PRINT"(((((":FORI=13TO17:LOCATE6,I:PRINT"P":NEXT:LOCATE6,18:PRINT"Y"
194 LOCATE6,12:PRINT"X":LOCATE4,17:PRINT"&&":LOCATE1,12:PRINT"ZZ":LOCATE1,13:PRINT"``":LOCATE1,14:PRINT"((!":LOCATE1,9:PRINT"((((!":LOCATE15,18:PRINT"ZZZ":LOCATE15,19:PRINT"```":LOCATE14,20:PRINT"#(((!":LOCATE22,13:PRINT"#(((((((("
195 LOCATE28,11:PRINT"ZZ":LOCATE28,12:PRINT"``":LOCATE13,4:PRINT"x((((yx((((yx(((((":LOCATE27,7:PRINT"#((!":LOCATE24,14:PRINT"& & &&&"
196 RETURN
197 '** Stage 16: Last stage
198 X=16:Y=168:Z=0:PUTSPRITE0,,,0:LOCATE18,3:PRINT"x((((((((((y":LOCATE14,1:PRINT"h":LOCATE14,7:PRINT"Z":LOCATE14,8:PRINT"`":LOCATE3,9:PRINT"P(((((!#(((((!":LOCATE3,10:PRINT"P&& &"
199 LOCATE3,11:PRINT"P":LOCATE3,12:PRINT"P":LOCATE21,13:PRINT"#(((((!":LOCATE1,15:PRINT"(((((((! #(((((('":LOCATE26,16:PRINT"#((((":LOCATE1,16:PRINT"&&& &":LOCATE13,16:PRINT"&& &":LOCATE5,22:PRINT"$$(((($$((((``(%('"
200 LOCATE17,21:PRINT"ZZ %('  ZZZ  %":LOCATE21,17:PRINT"$":FORI=18TO20:LOCATE20,I:PRINT"%('":NEXT:FORI=18TO22:LOCATE30,I:PRINT"%":LOCATE25,22:PRINT"```":NEXT:LOCATE3,8:PRINT"X":LOCATE3,13:PRINT"Y":PUTSPRITE4,(200,14),15,1:PUTSPRITE5,(207,12),8,2
201 RETURN
202 '** Game win / ending
203 CLS:LOCATE0,0:FORI=0TO8:PRINT"kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk";:NEXT:PRINT"kwrtpukkkkkkkwrtpukkkkkkkwrtpukkrs   vpqrtpqrs   vpqrtpqrs   vpq":LOCATE0,22:PRINT"((((((((((((((((((((((((((((((((???????????????????????????????";
204 '* VDP(1)=103 sets sprites to double size for 16x16 sprites, VDP(1)=102 will set zoom off.
205 VDP(1)=103:X=-66:Y=157:Z=0:PUTSPRITE0,(0,192),,1:PUTSPRITE4,(123,157),15,1:PUTSPRITE5,(137,153),8,2
206 PUTSPRITE0,(X\2,Y):IFX<202THENX=X+1:GOTO206
207 PUTSPRITE0,,,0:FORI=0TO9:A=2+(I*3):B=6-(IMOD2)*4:LOCATEA,B:PRINT"h":PLAY"o5gab":TIME=0
208 IFTIME>20THENLOCATEA-1,B-1:PRINT"jij":LOCATEA-1,B:PRINT"ihi":LOCATEA-1,B+1:PRINT"jij"
209 IFTIME>40THENNEXTELSEGOTO208
210 PUTSPRITE4,,,0:FORI=0TO6:TIME=0
211 IFTIME<20THEN211
212 IFI=0THENLOCATE15,18:PRINT"\]":PLAY"o4d":IFK=0THENI=6:NEXTELSENEXT
213 IFI=1THENLOCATE14,17:PRINT"\]\]":PLAY"e":NEXT
214 IFI=2THENLOCATE13,16:PRINT"\]\]\]":PLAY"f":NEXT
215 IFI=3THENLOCATE12,15:PRINT"\]\]\]\]":PLAY"g":NEXT
216 IFI=4THENLOCATE13,14:PRINT"\]\]\]":PLAY"a":NEXT
217 IFI=5THENLOCATE12,13:PRINT"\]\]\]\]":PLAY"b":NEXT
218 IFI=6THENLOCATE13,12:PRINT"\]  \]":PLAY"o5c":NEXT
219 PLAY"t255o4grgro5drdrererddrrcrcro4brbrararggrro5drdrcrcro4brbraarro5drdrcrco4rbrbraarrgrgro5drdrererddrrcrcro4brbrararggg","t255o2ggrro3ddrreerrddrrccrro2bbrraarrggrro3ddrrccrro2bbrraarro3ddrrccro2rbbrraarrggrro3ddrreerrddrrccrro2bbrraarrggg"
220 LOCATE5,14:PRINT"OHM?BHL?@FF?LCA?KL@JK=":TIME=0
221 IFTIME>90THENLOCATE7,16:PRINT"LJMA?FHNA?DK?OHMJK="ELSEGOTO221
222 IFTIME>260THENPLAY"o4l6t255brgrggggg","o3l6t255drbrbbbbb"ELSEGOTO222
223 IFK=0THENLOCATE3,23:PRINT"IJH?B@GA?C@K?GHJA?KL@BAK=";ELSELOCATE3,23:PRINT"OHM?@JA?@?CAJH>FANAF?B@GAJ=";
224 IFTIME>750THENRESTORE:GOTO1:ELSEGOTO224
