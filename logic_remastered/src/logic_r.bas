40 defint A-Z

70 GOTO 901

120 'START GAME
121 FOR I=0 TO 31:PUT SPRITE I,((I MOD 16)*20,164+(I\16)*16),0,0:NEXT I
122 CALL TURBO ON(S,TR)
130 R=0:C=1:PL=9

150 SS=0:SJ=0:PS%=0:SX=46:SY=80:SV=0:TH=0:TV=0

151 IF R=0 AND C=9 GOTO 800
155 A$=CHR$(34)+"map_"+CHR$(C+48)+"_"+CHR$(R+48)+".scr"+CHR$(34)+",S"+CHR$(0)
156 '#I &h21,A$,&h23,&hdd,&h2a,&hCA,&h39,&hcd,&h59,&h01

158 VPOKE &H1AC6,48+PL
159 GOSUB 670

160 'Load enemies for this room
180 'ON SPRITE GOSUB 541

199 X=SX:Y=SY:VY=SV:PJ=SJ:TH=0:TV=0:PS=SS:SPRITE OFF

200 TIME=0
201 ON STICK(S) GOTO 210,220,230,240,250,260,270,280
209 GOTO 300
210 IF PJ%=0 THEN VY%=-8:PJ%=1:IF PS%>=2 THEN PS%=2 ELSE PS%=0
219 GOTO 300
220 IF PJ%=0 THEN VY%=-8:PJ%=1
221 TH=VPEEK(&H1800+(X+16)\8+((Y+8)\8)*32)
222 PS%=0:IF VPEEK(&H1800+(X+16)\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+(X+16)\8+((Y+2)\8)*32)=0 THEN X=X+4 ELSE X=(X\4)*4+2
229 GOTO 300
230 TH=VPEEK(&H1800+(X+16)\8+((Y+8)\8)*32)
231 IF VPEEK(&H1800+(X+16)\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+(X+16)\8+((Y+2)\8)*32)=0 THEN X=X+4 ELSE X=(X\4)*4+2
232 IF PS%=0 AND PJ%=0 THEN PS%=1 ELSE PS%=0
239 GOTO 300
240 IF PJ%=0 THEN VY%=-5:PJ%=1
241 TH=VPEEK(&H1800+(X+16)\8+((Y+8)\8)*32)
242 PS%=0:IF VPEEK(&H1800+(X+16)\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+(X+16)\8+((Y+2)\8)*32)=0 THEN X=X+4 ELSE X=(X\4)*4+2
249 GOTO 300
250 IF PJ%=0 THEN VY%=-5:PJ%=1:IF PS%>=2 THEN PS%=2 ELSE PS%=0
259 GOTO 300
260 IF PJ%=0 THEN VY%=-5:PJ%=1
261 TH=VPEEK(&H1800+X\8+((Y+8)\8)*32)
262 PS%=2:IF VPEEK(&H1800+X\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+X\8+((Y+2)\8)*32)=0 THEN X=X-4 ELSE X=(X\4)*4+2
269 GOTO 300
270 TH=VPEEK(&H1800+X\8+((Y+8)\8)*32)
271 IF VPEEK(&H1800+X\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+X\8+((Y+2)\8)*32)=0 THEN X=X-4 ELSE X=(X\4)*4+2
272 IF PS%=2 AND PJ%=0 THEN PS%=3 ELSE PS%=2
279 GOTO 300
280 IF PJ%=0 THEN VY%=-8:PJ%=1
281 TH=VPEEK(&H1800+X\8+((Y+8)\8)*32)
282 PS%=2:IF VPEEK(&H1800+X\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+X\8+((Y+2)\8)*32)=0 THEN X=X-4 ELSE X=(X\4)*4+2
289 GOTO 300
300 'End of movement loop 
301 'Check for floor tiles at 3 points under the sprite Y+16,(X, X+8, X+15)'
310 Y=Y+VY%
320 IF VY%<0 GOTO 340
330 TV=VPEEK(&H1800+(X+8)\8+((Y+16)\8)*32)
331 IF VPEEK(&H1800+(X+2)\8+((Y+16)\8)*32)>0 OR TV>0 OR VPEEK(&H1800+(X+12)\8+((Y+16)\8)*32)>0 THEN PJ%=0:VY%=0:Y=Y-(Y MOD 8) ELSE PJ%=-1:VY%=VY%+1:IF VY%>8 THEN VY%=8
339 GOTO 350
340 TV=VPEEK(&H1800+(X+8)\8+(Y\8)*32)
341 IF VPEEK(&H1800+(X+2)\8+(Y\8)*32)>0 OR TV>0 OR VPEEK(&H1800+(X+12)\8+(Y\8)*32)>0 THEN VY%=0:Y=Y+8-(Y MOD 8) ELSE VY%=VY%+1:IF VY%>8 THEN VY%=8
350 IF TV<128 OR TV>142 THEN GOTO 355
351   TH=VPEEK(&H1800+(X+16)\8+((Y+8)\8)*32)
352   IF VPEEK(&H1800+(X+16)\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+(X+16)\8+((Y+2)\8)*32)=0 THEN X=X+4 ELSE X=(X\4)*4+2
355 IF TV<143 OR TV>157 THEN GOTO 360
356   TH=VPEEK(&H1800+X\8+((Y+8)\8)*32)
357   IF VPEEK(&H1800+X\8+((Y+12)\8)*32)=0 AND TH=0 AND VPEEK(&H1800+X\8+((Y+2)\8)*32)=0 THEN X=X-4 ELSE X=(X\4)*4+2
360 IF (TV>=96 AND TV<=125) OR (TH>=96 AND TH<=125) THEN GOTO 530
363 IF (TV>=160 AND TV<=183) OR (TH>=160 AND TH<=183) THEN GOSUB 550
364 IF (TV>=192 AND TV<=215) OR (TH>=192 AND TH<=215) THEN GOSUB 600
380 IF X>=226 THEN X=18:C=C+1:GOTO 700
381 IF X<=14 THEN X=222:C=C-1:GOTO 700
382 IF Y>=112 THEN Y=4:R=R+1:GOTO 700
383 IF Y<=2 THEN Y=110:R=R-1:GOTO 700
390 PUT SPRITE 0,(X,Y-1),6,PS%
490 IF TIME<5 GOTO 490
499 GOTO 200

530 'Lost a life
531 IF TR=0 THEN PL=PL-1
532 IF PL=-1 THEN R=3:C=10:GOTO 800
533 'Update lifes UI
534 VPOKE &H1AC6,48+PL
535 K=11
536 TIME=0
537 K=K+1: IF K=19 THEN 199
538 PUT SPRITE 0,(X,Y-1),6,K
539 IF TIME<10 THEN 539 ELSE 536

541 'Sprite collision, lose a life
542 SPRITE OFF
549 RETURN 530

550 'Get a key
550 IF TV>=160 AND TV<=183 THEN K=TV\8-20 ELSE K=TH\8-20
551 IF VPEEK(&H1A42+2*K)<200 THEN RETURN
560 VPOKE &H1A42+2*K, 20+4*K
561 VPOKE &H1A43+2*K, 21+4*K
562 VPOKE &H1A62+2*K, 22+4*K
563 VPOKE &H1A63+2*K, 23+4*K
590 RETURN

600 'Open a lock
600 IF TV>=192 AND TV<=215 THEN K=TV\8-24 ELSE K=TH\8-24
610 IF VPEEK(&H1A42+2*K)>31 THEN RETURN
660 VPOKE &H1A42+2*K, 32+4*K
661 VPOKE &H1A43+2*K, 33+4*K
662 VPOKE &H1A62+2*K, 34+4*K
663 VPOKE &H1A63+2*K, 35+4*K
664 GOSUB 670
665 RETURN

670 'Remap those tiles to empty (read the info from invisible bricks)
670 L=VPEEK(&H1800)-23
671 IF L<=0 THEN RETURN
672 K=VPEEK(&H1A40+2*L)
680 IF K<31 OR K>200 THEN RETURN
681 IF L=1 THEN FOR I=0 TO 3:VPOKE &H19C6+I,0:VPOKE &H19E6+I,0:NEXT I
682 IF L=2 THEN FOR I=0 TO 3:VPOKE &H191A+I,0:VPOKE &H193A+I,0:VPOKE &H195A+I,0:VPOKE &H197A+I,0:VPOKE &H199A+I,0:VPOKE &H19BA+I,0:NEXT I
683 IF L=3 THEN FOR I=0 TO 3:VPOKE &H195A+I,0:VPOKE &H197A+I,0:VPOKE &H199A+I,0:VPOKE &H19BA+I,0:NEXT I
690 RETURN

700 'Move to another room
701 FOR I=0 TO 4:PUT SPRITE I,(I*20,132),0,0:NEXT I
705 SPRITE OFF
710 SX=X:SY=Y:SV=VY:SJ=PJ:SS=PS
790 GOTO 151

800 'End of game (either)
800 CALL TURBO OFF
801 FOR I=0 TO 4:PUT SPRITE I,(I*20,132),0,0:NEXT I
802 IF R=0 AND C=9 GOTO 850
810 BLOAD "over.scr",S
811 IF INKEY$<>"" GOTO 811
812 IF INKEY$="" GOTO 812
830 GOTO 900

850 'Ending Screen
850 BLOAD "end.scr",S
852 TIME=0
853 TX=1:TY=0:T$="YOU MADE IT^^":GOSUB 890
854 TX=1:TY=2:T$="YOU GOT OUT ALIVE^^":GOSUB 890
855 TX=1:TY=4:T$="BUT[[[":GOSUB 890
856 IF TIME<150 GOTO 856
857 IF TR=1 THEN TX=1:TY=6:T$="CHEATS INVALIDATE IT^":GOSUB 890:GOTO 870
858 TX=1:TY=6:T$="WHY TOOK YOU SO LONG]":GOSUB 890
859 IF TIME<250 GOTO 859
860 TX=1:TY=9:T$="DOES NOT MATTER BECAUSE":GOSUB 890
861 TX=1:TY=11:T$="IT DOES NOT END HERE":GOSUB 890
862 IF TIME<400 GOTO 862
863 TX=1:TY=14:T$="STAY TUNED[[[":GOSUB 890
870 IF INKEY$<>"" GOTO 870
871 IF INKEY$="" GOTO 871
875 GOTO 900

890 FOR I=1 TO LEN(T$)
891   CT$=MID$(T$,i,1)
892   VPOKE &H1800+TX+TY*32+I,ASC(CT$)+159
893 NEXT I
894 RETURN

900 'Start New Game Screen
900 CALL TURBO OFF
901 BLOAD "start.scr",S
910 I=20:J=1:K=4:TR=0
940 IF J=1 THEN I=I+4 ELSE I=I-4
941 TIME=0
960 PUT SPRITE 1,(8,I),8-TR,K:PUT SPRITE 2,(231,I),8-TR,K
970 K=K+1:IF K=8 THEN K=4
980 IF I=140 THEN J=0:TR=0
981 IF I=20 THEN J=1
990 IF STRIG(0) THEN S=0: GOTO 120
991 IF STRIG(1) THEN S=1: GOTO 120
992 ' If trick is enabled, show the feedback on screen
993 IF TIME<5 GOTO 990
997 IF INKEY$="O" THEN run"logic_o.bas"
998 IF INKEY$="R" THEN TR=1
999 GOTO 940